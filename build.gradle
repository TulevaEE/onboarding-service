buildscript {
	ext {
		springBootVersion = '2.1.4.RELEASE'
		springBootAdminVersion = '2.1.3'
		springCloudSleuthVersion = '2.1.1.RELEASE'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.0.0")
	}
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'com.gorylenko.gradle-git-properties'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven {
		url "https://raw.githubusercontent.com/TulevaEE/releases-repo/master/"
	}
	maven {
		url "https://jitpack.io"
	}
}

dependencies {
	compileOnly('org.projectlombok:lombok:1.18.6')
	annotationProcessor('org.projectlombok:lombok:1.18.6')
	runtime("org.springframework.boot:spring-boot-devtools")
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation('org.springframework.boot:spring-boot-starter-security')
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
	implementation('org.springframework.boot:spring-boot-starter-cache')
	implementation('org.springframework.boot:spring-boot-starter-aop')
	implementation('org.springframework.boot:spring-boot-starter-json')
	implementation('org.springframework.cloud:spring-cloud-starter-sleuth')
	implementation('de.codecentric:spring-boot-admin-starter-client')
	implementation('org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.1.4.RELEASE')

	runtimeOnly('org.postgresql:postgresql')

	implementation('org.flywaydb:flyway-core')
	implementation("org.springframework.boot:spring-boot-starter-data-jpa")
	implementation('org.glassfish.jersey.inject:jersey-hk2:2.28')
	implementation('org.jsoup:jsoup:1.8.1')
	implementation('org.apache.commons:commons-lang3:3.8.1')
	implementation('net.sf.ehcache:ehcache')
	implementation('org.decampo:xirr:0.0')
	implementation('org.bouncycastle:bcpkix-jdk15on:1.61')

	implementation('com.github.ErkoRisthein:mobileid:1.9') {
		exclude group: 'commons-logging', module: 'commons-logging'
	}
	implementation('ee.sk.smartid:smart-id-java-client:1.1')

	implementation('io.sentry:sentry-logback:1.7.16')
	implementation('io.springfox:springfox-swagger2:2.6.1')
	implementation('io.springfox:springfox-swagger-ui:2.6.1')

	implementation('org.hibernate:hibernate-java8')
	implementation('com.mandrillapp.wrapper.lutung:lutung:0.0.7')

	implementation('com.ecwid:maleorang:3.0-0.9.6')

	implementation('com.sun.xml.ws:jaxws-rt:2.2.10')

	testImplementation("com.h2database:h2")
	testImplementation("org.springframework.boot:spring-boot-starter-test") {
		exclude module: "spock-core"
	}
	testImplementation('org.spockframework:spock-core:1.3-groovy-2.4')
	testImplementation('org.spockframework:spock-spring:1.3-groovy-2.4')
	testImplementation('com.github.tomakehurst:wiremock:2.4.1')
	testImplementation('org.springframework.security:spring-security-test')
}

dependencyManagement {
	imports {
		mavenBom("de.codecentric:spring-boot-admin-dependencies:$springBootAdminVersion")
		mavenBom("org.springframework.cloud:spring-cloud-sleuth:$springCloudSleuthVersion")
	}
}

test {
	testLogging {
		events "passed", "skipped", "failed", "standardOut", "standardError"
		showCauses true
		showExceptions true
		showStackTraces true
		exceptionFormat = 'full'
	}
}

bootRun {
	systemProperties['spring.profiles.active'] = 'dev'
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
		html.enabled false
	}
}

check.dependsOn jacocoTestReport

task zipWithExtensions(type: Zip) {
	dependsOn jar
	from(jar.outputs.files.singleFile) {
		rename { "application.jar" }
	}
	from("$rootDir/etc/eb/.ebextensions") {
		into(".ebextensions")
	}
	from("$rootDir/etc/eb/Procfile")
}

assemble.dependsOn zipWithExtensions
